group = "com.adaptc.gradle"

apply plugin:'idea'

subprojects { proj ->
	apply plugin:'java'
	apply plugin:'groovy'
	apply plugin:'maven'
	apply plugin:'signing'
	apply plugin:'eclipse'
	apply plugin:'idea'

	configurations {
		deployerJars
	}

	configurations.all {
		// check for updates every build
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		deployerJars "org.apache.maven.wagon:wagon-http:1.0-beta-2"
		testCompile ("org.spockframework:spock-core:0.7-groovy-1.8") {
			exclude module:"groovy-all"
		}
		compile gradleApi()
		groovy localGroovy()
		compile ('org.codehaus.groovy.modules.http-builder:http-builder:0.5.2') {
			exclude group:'org.codehaus.groovy', module:'groovy'
		}
		compile 'com.mapvine:gradle-cobertura-plugin:1.0'
		compile 'commons-lang:commons-lang:2.6'
		runtime 'commons-collections:commons-collections:3.2.1'
		runtime 'commons-beanutils:commons-beanutils:1.8.3'
		runtime 'net.sf.ezmorph:ezmorph:1.0.6'
	}

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"
	if (proj.name=="commons" || proj.name=="testing")
		proj.version = rootProject.'commons.version'

	manifest.mainAttributes(
			"Built-By": System.properties['user.name'],
			"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
			"Implementation-Title": "Adaptive Computing Plugins Commons - "+proj.archivesBaseName,
			"Implementation-Version": proj.version,
			"Implementation-Vendor": 'adaptivecomputing.com')

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives jar
		archives sourcesJar
		archives javadocJar
	}

	signing {
		sign configurations.archives
	}

	def isSnapshot = proj.version.contains("-SNAPSHOT")

	uploadArchives {
		def repo = "oss-" + (isSnapshot ? "snapshots" : "releases")
		def repoUrl = rootProject."${repo}.url"
		def username = rootProject."${repo}.username"
		def password = rootProject."${repo}.password"

		repositories.mavenDeployer {
			if (!isSnapshot) {
				beforeDeployment { deployment -> signPom(deployment) }
			}

			configuration = configurations.deployerJars
			repository(url:repoUrl) {
				authentication(userName:username, password:password)
			}

			pom {
				project {
					name proj.projectName
					description proj.projectDescription
					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}
					url "http://github.com/adaptivecomputing/plugins-commons"
					scm {
						connection "scm:git:https://github.com/adaptivecomputing/plugins-commons.git"
						developerConnection "scm:git:https://github.com/adaptivecomputing/plugins-commons.git"
						url "http://github.com/adaptivecomputing/plugins-commons"
					}
					developers {
						developer {
							id "bsaville"
							name "Brian Saville"
							email "bsaville@adaptivecomputing.com"
							organization = "Adaptive Computing, Inc."
							organizationUrl "http://adaptivecomputing.com"
							timezone "-7"
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.3'
	jarFile = 'wrapper/wrapper.jar'
}